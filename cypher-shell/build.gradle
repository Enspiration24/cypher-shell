version = buildVersion

task generateBuildProperties {
  def outputDir = file("${buildDir}/resources/main")
  doFirst {
    outputDir.exists() || outputDir.mkdirs()
    def propFile = new File(outputDir, "build.properties")
    def props = new Properties()
    props.setProperty("version", buildVersion)
    props.setProperty("driverVersion", neo4jJavaDriverVersion)
    props.store(propFile.newWriter(), null)
  }
}

processResources.finalizedBy generateBuildProperties

jar {
  manifest {
    attributes 'Main-Class': 'org.neo4j.shell.Main'
  }
}

shadowJar {
  // Insure no version number is included in jar name
  version = null
}

distributions {
  main {
    baseName = 'cypher-shell'
    contents {
      from {'src/dist'}
      from shadowJar
    }
  }
}

dependencies {
    compile "net.sourceforge.argparse4j:argparse4j:$argparse4jVersion"
    compile("org.neo4j:neo4j-cypher-expression-evaluator:$evaluatorVersion") {
        exclude(group: 'org.neo4j', module: 'neo4j-index')
        exclude(group: 'org.neo4j', module: 'neo4j-fulltext-index')
        exclude(group: 'org.neo4j', module: 'neo4j-ssl')
        exclude(group: 'org.neo4j', module: 'neo4j-graph-algo')
        exclude(group: 'org.neo4j', module: 'neo4j-id-generator')
        exclude(group: 'org.neo4j', module: 'neo4j-io')
        exclude(group: 'org.neo4j', module: 'neo4j-label-index')
        exclude(group: 'org.neo4j', module: 'neo4j-storage-engine-api')
        exclude(group: 'org.neo4j', module: 'neo4j-spatial-index')
        exclude(group: 'org.neo4j', module: 'neo4j-wal')
        exclude(group: 'org.neo4j', module: 'neo4j-native')
        exclude(group: 'org.neo4j', module: 'neo4j-logging')
        exclude(group: 'org.neo4j', module: 'neo4j-procedure-api')
        exclude(group: 'org.eclipse.collections')
        exclude(group: 'org.jctools')
    }
    compile "org.neo4j.driver:neo4j-java-driver:$neo4jJavaDriverVersion"
    compileOnly "com.google.code.findbugs:annotations:$findbugsVersion"
    compile "org.fusesource.jansi:jansi:$jansiVersion"
    compile("jline:jline:$jlineVersion") {
        exclude(group: 'junit', module: 'junit')
    }
    compile group: 'commons-io', name: 'commons-io', version: "$commonsIoVersion"
    testCompile "junit:junit:$junitVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile "com.github.stefanbirkner:system-rules:$systemRulesVersion"
    testCompileOnly "com.google.code.findbugs:annotations:$findbugsVersion"
}
